#!/usr/bin/perl -w
use strict;
use autodie;
use IPC::System::Simple qw(capture);
use FindBin qw($Bin);

# Builds the AUTHORS file which lists all project contributors.

my $filename = $ARGV[0] || "$Bin/../AUTHORS";

open(my $authors_fh, '>', $filename);

my @logs = capture(qw(git log));

my %author_email;

foreach (@logs) {
    $author_email{$1} = $2 if /^Author: (.*?)\s*<(.*)>/;
}

foreach my $name (sort keys %author_email) {

    my $formatted_name = $name;

    $formatted_name =~ s{LesleyB}   {Lesley Binks};
    $formatted_name =~ s{spiros}    {Spiros Denaxas};

    $_ = sprintf "%-30s<%s>\n", $formatted_name, $author_email{$name};

    # Fix e-mail addresses
    s{ben\.hengst\@gmail\.com}   {notbenh\@cpan.org};
    s{spiros\@karoto\.config}    {spiros\@lokku.com};

    print {$authors_fh} $_;
}

close($authors_fh);
